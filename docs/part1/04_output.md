# Outputs

!!! info "Learning objectives"

    1. Utlizie Nextflow process output blocks
    2. Publish results from your pipeline with directives

Currently, our pipeline is simply printing 'Hello World!' to the terminal via the standard output (`stdout`). This isn't particularly useful if we want to do anything with the outputs of our processes. Instead, we can save the output of our process to a file that can be passed on to other processes later on. In a "real-world" pipeline, this is like having a command that specifies an output file as part of its normal syntax.

Here you're going to update the `script` and the `output` definition blocks to save the 'Hello World!' as an output.

## Redirecting outputs

The script block will need to be updated to **redirect** the 'Hello World!' output to a file.

The `>` operator can be used for output redirection.

!!!question "Exercise"

    Redirect 'Hello World!' to a file named 'output.txt' in the `script` block and add a comment to annotate your change.

    ??? "Solution"

        ```groovy title="hello-world.nf" hl_lines="1 10"
        // Use echo to print 'Hello World!' and redirect to output.txt
        process SAYHELLO {
            debug true

            output:
            stdout

            script:
            """
            echo 'Hello World!' > output.txt
            """
        }
        ```

## Capturing outputs

We have now updated our script to write 'Hello World!' to `output.txt`, but we also need to tell Nextflow to expect this file - otherwise, it will ignore it! Nextflow requires us to **declare** what outputs should be captured from each process. This is particularly useful for a number of reasons. First, many tools will generate intermediate files that we don't need, and capturing all of them would be messy and unnecessary. Second, Nextflow uses the outputs we declare to figure out how and when to run each process. And finally, by declaring our process outputs, Nextflow has a way to determine whether our process succeeded or not; if an output is declared but is missing at the end of the process, Nextflow will assume it has failed.

We declare our outputs using the `output` definition block. Typically this will require both an **output qualifier** and an **output name**:

```groovy
output:
<output qualifier> <output name>
```

The **output qualifier** defines the type of data to be received. This information is used by Nextflow to apply the semantic rules associated with each qualifier, and handle it properly.

Common output qualifiers include `val` and `path`:

- `val`: Emit the variable with the specified name
  - For example, `val 'Hello World!'`
- `path`: Emit a file produced by the process with the specified name
  - For example, `path 'output.txt'`

See the [Nextflow documentation](https://www.nextflow.io/docs/latest/process.html#outputs) for a full list of output qualifiers.

!!!warning

    If you set the wrong qualifier the pipeline will likely throw errors.

The **output name** is a name given to the output variable. If a specific file is being produced it can be named in single quotes:

```groovy title="hello-world.nf"
output:
path 'output.txt'
```

The output name and the file generated by the script must exactly match (or be picked up by a glob pattern), or else Nextflow won't find it and will throw an error.

!!! note

    It is important to understand that the `output` block does not *determine* the output of the process. Instead, it simply *declares* what output should be expected. It is up to the logic inside the `script` block to ensure that the file is actually being created.

So far, we have been using the `stdout` output declaration, which tells Nextflow to capture all of the information sent to the standard output. This is a special output qualifier in that it doesn't require an output name to go along with it.

Now that we are redirecting our 'Hello World!' message to a file, we want to tell nextflow to expect an output file called `output.txt`.

!!!question "Exercise"

    Add `path 'output.txt'` in the `SAYHELLO` output block.

    ???Solution

        ```groovy title="hello-world.nf" hl_lines="6"
        // Use echo to print 'Hello World!' and redirect to output.txt
        process SAYHELLO {
            debug true

            output:
            path 'output.txt'

            script:
            """
            echo 'Hello World!' > output.txt
            """
        }
        ```

!!! warning

    This example is brittle because the output filename is hardcoded in two separate places (the `script` and the `output` definition blocks). If you change one but not the other, the script will break.

## Publish outputs

By default, all files created by processes exist only inside the `work` directory. To make our outputs more accessible and neatly organised, we define a **publishing strategy**, which determines which outputs should be copied to a final **publishing directory**.

The [`publishDir` directive](https://www.nextflow.io/docs/latest/process.html#publishdir) can be used to specify where and how output files should be saved. For example:

```groovy
publishDir 'results'
```

By adding the above to a process, all output files would be saved in a new folder called `results` in the current working directory. The `publishDir` directive is process specific.

!!!question "Exercise"

    Replace `debug true` with `publishDir 'results'` in the `SAYHELLO` process block. Execute the pipeline again. View your new `results` folder in the working directory.

    ???Solution

        ```groovy title="hello-world.nf" hl_lines="3"
        // Use echo to print 'Hello World!' and redirect to output.txt
        process SAYHELLO {
            publishDir 'results'

            output:
            path 'output.txt'

            script:
            """
            echo 'Hello World!' > output.txt
            """
        }
        ```

!!! abstract "Summary"

    In this step you have learned:

    1. How to redirect outputs
    2. How to use `output` block
    3. How to publish results
